ext {
  WPILib_version = '2017.3.1'
  OpenCV_version = '3.1.0'
  NetworkTables_version = '3.1.7'
  CameraServer_version = '1.0.2'
}

buildscript {
  ext {
    kotlin_version = '1.1.2-2'
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

apply plugin: 'kotlin'

sourceSets.main.kotlin.srcDirs = ['src']

repositories {
  mavenCentral()
  maven {
    url 'http://first.wpi.edu/FRC/roborio/maven/release'
  }
}

dependencies {
  compile 'org.jetbrains.kotlin:kotlin-stdlib'
  compile "edu.wpi.first.wpilibj:athena:$WPILib_version"
  compile "org.opencv:opencv-java:$OpenCV_version"
  compile "edu.wpi.first.wpilib.networktables.java:NetworkTables:$NetworkTables_version:arm"
  compile "edu.wpi.cscore.java:cscore:$CameraServer_version:arm"

  runtime "edu.wpi.first.wpilib:athena-runtime:$WPILib_version"
  runtime "edu.wpi.first.wpilibj:athena-jni:$WPILib_version"
  runtime "org.opencv:opencv-natives:$OpenCV_version:linux-arm"
  runtime "org.opencv:opencv-jni:$OpenCV_version:linux-arm"
  runtime "edu.wpi.cscore.java:cscore:$CameraServer_version:athena-uberzip@zip"
}

jar {
  from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  manifest {
    attributes 'Main-Class': 'edu.wpi.first.wpilibj.RobotBase'
    attributes 'Robot-Class': 'com.team5499.robots.frc2017.Robot'
  }
}

task installRuntimeLibraries(type: DefaultTask) {
  configurations.runtime.asFileTree.each { lib ->
    copy {
      from zipTree(lib)
      into 'build/runtimeLibraries'
      include '**/*.so*'
      includeEmptyDirs = false
      eachFile { details ->
        details.setRelativePath(new RelativePath(true, details.getName()))
      }
    }
  }
  exec {
    commandLine 'rsync'
    args '-c', '-z', '-r', '-v', 'build/runtimeLibraries/', 'admin@roborio-vm:/usr/local/frc/lib'
  }
}
